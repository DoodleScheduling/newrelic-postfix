apply plugin: 'idea'
apply plugin: 'java'

version = "1.0.0"

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
    module {
        excludeDirs = [file(".idea"), file(".gradle"), file("dist")]
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "ch.qos.logback:logback-classic:1.1.7"
    compile "ch.qos.logback:logback-core:1.1.7"
    compile "com.googlecode.json-simple:json-simple:1.1.1"
    compile "org.slf4j:slf4j-api:1.7.21"
    testCompile "junit:junit:4.12"
    testCompile "org.objenesis:objenesis:2.4"
    testCompile "org.hamcrest:hamcrest-core:1.3"
    testCompile "org.easymock:easymock:3.4"
    testCompile "cglib:cglib-nodep:3.2.4"
}

test {
    test.maxParallelForks = Runtime.runtime.availableProcessors();
}

sourceSets {
    main {
        java {
            srcDirs 'src'
        }
    }
    test {
        java {
            srcDirs 'test'
            resources.srcDirs = ['test/resources']
        }
    }
}

jar {
    baseName = 'plugin'
    manifest {
        attributes 'Implementation-Title': 'Postfix New Relic Plugin',
                'Implementation-Version': version,
                'Main-Class': 'com.newrelic.metrics.publish.Plugin'
    }
    from {
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    destinationDir = file('dist')
}

task createDistributable(type: Tar, dependsOn: [jar]) {
    from "dist/plugin-${version}.jar"
    from "config"
    baseName = 'postfixNewRelic'
    destinationDir = file('dist')
    extension = 'tar.gz'
    compression = Compression.GZIP
}

